# Ada Support for Concurrency

This section describes the support for concurrency provided by Ada. Ada 83 supports only synchronous message passing.

## Fundamentals

The Ada design for tasks is partially based on the work of Brinch Hansen and Hoare in that message passing is the design basis and nondeterminism is used to choose among the tasks that have sent messages.

The full Ada tasking model is complex, and the following discussion of it is limited.

?> The focus here will be on the Ada version of the *synchronous message-passing* mechanism.

***ADA Tasks vs. Monitors***

Ada tasks can be more active than monitors.

- <mark>Monitors</mark> are *passive* entities that provide management services for the shared data they store. They <mark>provide their services</mark>, though only <mark>when those services are requested.</mark>
- When used to manage shared data, <mark>Ada tasks can be thought of as managers</mark> that can reside with the resource they manage. They have several mechanisms, some deterministic and some nondeterministic, that allow them to choose among competing requests for access to their resources.

***Two Syntactic Parts***

There are two syntactic parts to an Ada task and both with the same name.

- Specification part
- Body part

***Task Specification***

The interface of a task is its entry points or locations where it can accept messages from other tasks.

- Because these entry points are part of its interface, it is natural that they be listed in the specification part of a task.
- Because a rendezvous can involve an exchange of information, messages can have parameters; therefore, task entry points must also allow parameters, which must also be described in the specification part.

In appearance, a task specification is similar to the package specification for an abstract data type.

<div class="alert-example">

As an example of an Ada task specification, consider the following code, which includes a single entry point named `Entry_1`, which has an in-mode parameter:

```ada
task Task_Example is
    entry Entry_1(Item : in Integer);
end Task_Example;
```

</div>

***Task Body***

A task body must include some syntactic form of the entry points that correspond to the `entry` clauses in that task’s specification part.

- In Ada, these task body entry points are specified by clauses that are introduced by the `accept` reserved word.
- An `accept` clause is defined as the range of statements beginning with the `accept` reserved word and ending with the matching `end` reserved word.

***`accept` Clause***

`accept` clauses are themselves relatively simple, but other constructs in which they can be embedded can make their semantics complex. A simple `accept` clause has the following form:

```ada
accept entry_name (formal parameters) do
    ...
end entry_name;
```

These statements are together called the `accept` clause body.

- The `accept` entry name matches the name in an `entry` clause in the associated task specification part.
- The optional parameters provide the means of communicating data between the caller and the called task.
- The statements between the `do` and the `end` define the operations that take place during the rendezvous.
- During the actual rendezvous, the sender task is suspended.

---

Whenever an `accept` clause receives a message that it is not willing to accept, for whatever reason, the sender task must be suspended until the `accept` clause in the receiver task is ready to accept the message.

- Of course, the `accept` clause must also remember the sender tasks that have sent messages that were not accepted.
- For this purpose, each `accept` clause in a task has a queue associated with it that stores a list of other tasks that have unsuccessfully attempted to communicate with it.

<div class="alert-example">

The following is the skeletal body of the task whose specification was given previously:

```ada
task body Task_Example is
begin
    loop
        accept Entry_1(Item : in Integer) do
            ...
        end Entry_1;
    end loop;
end Task_Example;
```

The `accept` clause of this task body is the implementation of the `entry` named `Entry_1` in the task specification.

- If the execution of `Task_Example` begins and reaches the `Entry_1 accept` clause before any other task sends a message to `Entry_1`, `Task_Example` is suspended.
- If another task sends a message to `Entry_1` while `Task_Example` is suspended at its `accept`, a rendezvous occurs and the `accept` clause body is executed.
- Then, because of the loop, execution proceeds back to the `accept`. If no other task has sent a message to `Entry_1`, execution is again suspended to wait for the next message.

---

A rendezvous can occur in two basic ways in this simple example.

1. First, the receiver task, `Task_Example`, can be waiting for another task to send a message to the `Entry_1` entry. When the message is sent, the rendezvous occurs. This is the situation described earlier.

2. Second, the receiver task can be busy with one rendezvous, or with some other processing not associated with a rendezvous, when another task attempts to send a message to the same entry.

    In that case, the sender is suspended until the receiver is free to accept that message in a rendezvous. If several messages arrive while the receiver is busy, the senders are queued to wait their turn for a rendezvous.

The two rendezvous just described are illustrated with the timeline diagrams in Figure 13.4.

![](./img/13.4.jpg ':figure Two ways a rendezvous with Task_Example can occur.')

</div>

***Actor Tasks and Server Tasks***

- Tasks need not have entry points. Such tasks are called **actor tasks** because they do not wait for a rendezvous in order to do their work. Actor tasks can rendezvous with other tasks by sending them messages.
- In contrast to actor tasks, a task can have `accept` clauses but not have any code outside those `accept` clauses, so it can only react to other tasks. Such a task is called a **server task**.

***Asymmetric Message Passing in Ada vs. CSP***

- An Ada task that sends a message to another task must know the entry name in that task.
- However, the opposite is not true: A task entry need not know the name of the task from which it will accept messages.

This asymmetry is in contrast to the design of the language known as CSP (Communicating Sequential Processes).

- In CSP, which also uses the message-passing model of concurrency, tasks accept messages only from explicitly named tasks.
- The disadvantage of this is that libraries of tasks cannot be built for general use.

***Graphical Representation of Rendezvous***

The usual graphical method of describing a rendezvous in which task `A` sends a message to task `B` is shown in Figure 13.5.

![](./img/13.5.jpg ':figure Graphical representation of a rendezvous caused by a message sent from task A to task B.')

***Task Declaration***

Tasks are declared in the declaration part of a package, subprogram, or block. Statically created tasks begin executing at the same time as the statements in the code to which that declarative part is attached.

- For example, a task declared in a main program begins execution at the same time as the first statement in the code body of the main program.
- Task termination, which is a complex issue, is discussed later in this section.

***Handling Multiple Entry Points: `select` Construct***

Tasks may have any number of entries.

- The order in which the associated `accept` clauses appear in the task dictates the order in which messages can be accepted.
- If a task has more than one entry point and requires them to be able to receive messages in *any order*, the task <mark>uses a `select` statement</mark> to enclose the entries.

<div class="alert-example">

Suppose a task models the activities of a bank teller, who must serve customers at a walk-up station inside the bank and also serve customers at a drive-up window.

The following skeletal teller task illustrates a `select` construct:

```ada
task body Teller is
begin
    loop
        select
            accept Drive_Up(formal parameters) do
                ...
            end Drive_Up;
            ...
        or
            accept Walk_Up(formal parameters) do
                ...
            end Walk_Up;
            ..
        end select;
    end loop;
end Teller;
```

In this task, there are two `accept` clauses, `Walk_Up` and `Drive_Up`, each of which has an associated queue.

The action of the `select`, when it is executed, is to examine the queues associated with the two `accept` clauses.

- If one of the queues is empty, but the other contains at least one waiting message (customer), the `accept` clause associated with the waiting message or messages has a rendezvous with the task that sent the first message that was received.
- If both `accept` clauses have empty queues, the `select` waits until one of the entries is called.
- If both `accept` clauses have nonempty queues, one of the `accept` clauses is nondeterministically chosen to have a rendezvous with one of its callers.

The loop forces the `select` statement to be executed repeatedly, forever.

</div>

***Extended `accept` Clause***

The `end` of the `accept` clause marks the end of the code that assigns or references the formal parameters of the `accept` clause.

The code, if there is any, between an `accept` clause and the next or (`or` the `end select`, if the `accept` clause is the last one in the `select`) is called the **extended** `accept` **clause**.

- The extended `accept` clause is executed only after the associated (immediately preceding) `accept` clause is executed.
- This execution of the extended `accept` clause is not part of the rendezvous and can take place concurrently with the execution of the calling task.

The sender is suspended during the rendezvous, but it is put back in the ready queue when the end of the `accept` clause is reached.

If an `accept` clause has no formal parameters, the `do-end` is not required, and the `accept` clause can consist entirely of an extended `accept` clause. Such an `accept` clause would be used exclusively for synchronization. Extended `accept` clauses are illustrated in the `Buf_Task` task in [Section 13-6.3][].

[Section 13-6.3]: /notes/programming-language/程式語言概念/ch13/13-6?id=competition-synchronization

## Cooperation Synchronization

***`when` Clause***

Each `accept` clause can have a guard attached, in the form of a `when` clause, that can delay rendezvous. For example,

```ada
when not Full(Buffer) =>
    accept Deposit(New_Value) do
        ...
    end
```

An `accept` clause with a `when` clause is either open or closed.

- If the Boolean expression of the `when` clause is currently true, that `accept` clause is called **open**.
- If the Boolean expression is false, the `accept` clause is called **closed**.

An `accept` clause that does not have a guard is always open. An open `accept` clause is available for rendezvous; a closed `accept` clause cannot rendezvous.

---

Suppose there are several guarded `accept` clauses in a `select` clause. Such a `select` clause is <mark>usually placed in an infinite loop.</mark> The loop causes the `select` clause to be executed repeatedly, with each `when` clause evaluated on each repetition.

- Each repetition causes a list of open `accept` clauses to be constructed.
- If exactly one of the open clauses has a nonempty queue, a message from that queue is taken and a rendezvous takes place.
- If more than one of the open `accept` clauses has nonempty queues, one queue is chosen nondeterministically, a message is taken from that queue, and a rendezvous takes place.
- If the queues of all open clauses are empty, the task waits for a message to arrive at one of those `accept` clauses, at which time a rendezvous will occur.

***`else` Clause***

If a `select` is executed and every `accept` clause is closed, a run-time exception or error results.

- This possibility can be avoided either by making sure one of the `when` clauses is always true or by adding an `else` clause in the `select`.
- An `else` clause can include any sequence of statements, except an `accept` clause.

***`terminate` Clause***

A `select` clause may have a special statement, `terminate`, that is selected only when it is open and no other `accept` clause is open. A `terminate` clause, when selected, means that the task is finished with its job but is not yet terminated. Task termination is discussed later in this section.

## Competition Synchronization

The features described so far provide for cooperation synchronization and communication among tasks. Next, we discuss how *mutually exclusive access* to shared data structures can be enforced in Ada.

- If access to a data structure is to be controlled by a task, then mutually exclusive access can be achieved by declaring the data structure *within* a task. The semantics of task execution usually guarantees mutually exclusive access to the structure, <mark>because only one `accept` clause in the task can be active at a given time.</mark>
- The only exceptions to this occur when tasks are nested in procedures or other tasks.

    For example, if a task that defines a shared data structure has a nested task, that nested task can also access the shared structure, which could destroy the integrity of the data. Thus, tasks that are meant to control access to a shared data structure should not define tasks.

<div class="alert-example">

The following is an example of an Ada task that implements a monitor for a buffer.

The buffer behaves very much like the buffer in Section 13.3, in which synchronization is controlled with semaphores.

```ada
task Buf_Task is
    entry Deposit(Item : in Integer);
    entry Fetch(Item : out Integer);
end Buf_Task;

task body Buf_Task is
    Bufsize : constant Integer := 100;
    Buf : array (1..Bufsize) of Integer;
    Filled : Integer range 0..Bufsize := 0;
    Next_In,
    Next_Out : Integer range 1..Bufsize := 1;
begin
    loop
        select
            when Filled < Bufsize =>
                accept Deposit(Item : in Integer) do
                    Buf(Next_In) := Item;
                end Deposit;
                Next_In := (Next_In mod Bufsize) + 1;
                Filled := Filled + 1;
        or
            when Filled > 0 =>
                accept Fetch(Item : out Integer) do
                    Item := Buf(Next_Out);
                end Fetch;
                Next_Out := (Next_Out mod Bufsize) + 1;
                Filled := Filled - 1;
        end select;
    end loop;
end Buf_Task;
```

In this example, both `accept` clauses are extended. These extended clauses can be executed concurrently with the tasks that called the associated `accept` clauses.

The tasks for a producer and a consumer that could use `Buf_Task` have the following form:

```ada
task Producer;
task Consumer;

task body Producer is
    New_Value : Integer;
begin
    loop
        -- produce New_Value --
        Buf_Task.Deposit(New_Value);
    end loop;
end Producer;

task body Consumer is
    Stored_Value : Integer;
begin
    loop
        Buf_Task.Fetch(Stored_Value);
        -- consume Stored_Value --
    end loop;
end Consumer;
```

</div>

## Protected Objects

As we have seen, access to shared data can be controlled by enclosing the data in a task and allowing access only through task entries, which implicitly provide competition synchronization. One problem with this method is that it is difficult to implement the rendezvous mechanism efficiently.

Ada 95 protected objects provide an <mark>alternative method of providing competition synchronization</mark> that need not involve the rendezvous mechanism.

A protected object is not a task; it is more like a monitor, as described in Section 13.4.

- Protected objects can be accessed either by protected subprograms or by entries that are syntactically similar to the `accept` clauses in tasks.
- The protected subprograms can be either
    - protected procedures, which provide mutually exclusive read-write access to the data of the protected object
    - protected functions, which provide concurrent read-only access to that data.
- Entries differ from protected subprograms in that they can have guards.

---

- Within the body of a protected procedure, the current instance of the enclosing protected unit is defined to be a variable;
- Within the body of a protected function, the current instance of the enclosing protected unit is defined to be a constant, which allows concurrent read-only access.

---

Entry calls to a protected object provide synchronous communication with one or more tasks using the same protected object. These entry calls provide access similar to that provided to the data enclosed in a task.

---

<div class="alert-example">

The buffer problem that is solved with a task in the previous subsection can be more simply solved with a protected object. Note that this example does not include protected subprograms.

```ada
protected Buffer is
    entry Deposit(Item : in Integer);
    entry Fetch(Item : out Integer);
private
    Bufsize : constant Integer := 100;
    Buf : array (1..Bufsize) of Integer;
    Filled : Integer range 0..Bufsize := 0;
    Next_In,
    Next_Out : Integer range 1..Bufsize := 1;
end Buffer;

protected body Buffer is
    entry Deposit(Item : in Integer) when Filled < Bufsize is
    begin
        Buf(Next_In) := Item;
        Next_In := (Next_In mod Bufsize) + 1;
        Filled := Filled + 1;
    end Deposit;

    entry Fetch(Item : out Integer) when Filled > 0 is
    begin
        Item := Buf(Next_Out);
        Next_Out := (Next_Out mod Bufsize) + 1;
        Filled := Filled - 1;
    end Fetch;
end Buffer;
```

</div>

## Evaluation

Using the general message-passing model of concurrency to construct monitors is like using Ada packages to support abstract data types—both are tools that are more general than is necessary.

- Protected objects are a better way to provide synchronized access to shared data.

---

- In the absence of distributed processors with independent memories, the choice between monitors and tasks with message passing as a means of implementing synchronized access to shared data in a concurrent environment is somewhat a matter of taste.

    - However, in the case of Ada, protected objects are clearly better than tasks for supporting concurrent access to shared data.
    - Not only is the code simpler; it is also much more efficient.

- For distributed systems, message passing is a better model for concurrency, because it naturally supports the concept of separate processes executing in parallel on separate processors.
